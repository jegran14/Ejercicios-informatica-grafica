<!DOCTYPE html>
<html>

  <head>
  
    <meta charset="utf-8">
    <title> Informatica Grafica </title>
    <style>
      canvas {border: 1px solid black;background:black}
    </style>
      
    <script id="myVertexShader" type="x-shader/x-vertex">
        
      uniform   mat4 projectionMatrix;
      uniform   mat4 modelViewMatrix;
      uniform   mat3 normalMatrix;
        
      attribute vec3 VertexPosition;
      attribute vec3 VertexNormal;
      attribute vec2 VertexTexcoords;
        
      varying vec3 N, ec;
      varying vec2 texCoords;
        
      void main()  {
          
        N               = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        ec              = vec3(ecPosition);
          
        gl_Position     = projectionMatrix * ecPosition;
        texCoords       = VertexTexcoords;
          
      }
      
    </script>
      
    <script id="myFragmentShader" type="x-shader/x-fragment">
        
      precision mediump float;
        
      struct LightData {
        vec3 Position; // PosiciÃ³n en coordenadas del ojo
        vec3 La;       // Ambiente
        vec3 Ld;       // Difusa
        vec3 Ls;       // Especular
      };
      uniform LightData Light;
      
      struct MaterialData {
        vec3 Ka;       // Ambiente
        vec3 Kd;       // Difusa
        vec3 Ks;       // Especular
        float alpha;   // Brillo
      };
      uniform MaterialData Material;
      
      uniform float  alpha;
      uniform bool   tablero;
        
      varying vec3 N, ec;
      varying vec2 texCoords;
      
      vec3 phong (vec3 N, vec3 L, vec3 V) {
        
        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);
        
        float NdotL    = dot (N,L);
        
        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);
          
          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }
        
        return (ambient + diffuse + specular);
      }
      
      float isRed () {
          
        float size = 8.0;
        float res  = float(mod(floor(texCoords.s * size) +
                               floor(texCoords.t * size), 2.0) == 0.0);
          
        return (0.4*res);
      
      }
      
      void main() {
        
        vec3 n = normalize(N);
        vec3 L = normalize(Light.Position - ec);
        vec3 V = normalize(-ec);
        
        vec3 myColor;
          
        if (gl_FrontFacing)
          myColor = phong(n,L,V);
        else
          if (!tablero)
            myColor = phong(-n,L,V);
          
        myColor.r += (isRed() * float(tablero));
          
        gl_FragColor = vec4(myColor, alpha);
          
      }
      
    </script>
      
  </head>

  <body>
  
    Transparency: <input type="range" name="Transparency" min="0" max="100" value="50"><br>
        
    <canvas id="myCanvas" style="border: none;" width="800" height="400">
      Your browser does not support HTML5
    </canvas>
      
    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivas.js"></script>
    <script src="transparencia.js"></script>
      
  </body>
  
</html>
