<!DOCTYPE html>
<html>

  <head>

    <meta charset="utf-8">
    <title> Informática Gráfica </title>
    <style>
      canvas {border: 1px solid black;}
    </style>

    <script id="myVertexShader" type="x-shader/x-vertex">

      uniform   mat4 projectionMatrix;
      uniform   mat4 modelViewMatrix;
      uniform   mat3 normalMatrix;

      uniform float Scale;

      attribute vec3 VertexPosition;
      attribute vec3 VertexNormal;
      attribute vec2 VertexTexcoords;

      varying vec3 N, ec;
      varying vec2 texCoords;
      varying vec3 texCoords3;

      void main()  {

        N               = normalize(normalMatrix * VertexNormal);
        vec4 ecPosition = modelViewMatrix * vec4(VertexPosition,1.0);
        ec              = vec3(ecPosition);

        gl_Position     = projectionMatrix * ecPosition;
        texCoords       = VertexTexcoords;
        texCoords3      = VertexPosition * Scale;
      }

    </script>

    <script id="myFragmentShader" type="x-shader/x-fragment">

      precision mediump float;
      struct LightData {
        vec3 Position; // Posición en coordenadas del ojo
        vec3 La;       // Ambiente
        vec3 Ld;       // Difusa
        vec3 Ls;       // Especular
      };
      uniform LightData Light;

      struct MaterialData {
        vec3 Ka;       // Ambiente
        vec3 Kd;       // Difusa
        vec3 Ks;       // Especular
        float alpha;   // Brillo
      };
      uniform MaterialData Material;

      uniform sampler2D myTexture;
      uniform float     repetition;

      uniform int UseProcedural;

      uniform vec3 Color1;
      uniform vec3 Color2;

      varying vec3 N, ec;
      varying vec2 texCoords;
      varying vec3 texCoords3;

      vec4 permute(vec4 x)       { return mod(((x*34.0)+1.0)*x, 289.0); }
      vec4 taylorInvSqrt(vec4 r) { return 1.79284291400159 - 0.85373472095314 * r; }

      float snoise(vec3 v) {

        const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
        const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

        // First corner
        vec3 i  = floor(v + dot(v, C.yyy) );
        vec3 x0 =   v - i + dot(i, C.xxx) ;

        // Other corners
        vec3 g = step(x0.yzx, x0.xyz);
        vec3 l = 1.0 - g;
        vec3 i1 = min( g.xyz, l.zxy );
        vec3 i2 = max( g.xyz, l.zxy );

        //  x0 = x0 - 0. + 0.0 * C
        vec3 x1 = x0 - i1 + 1.0 * C.xxx;
        vec3 x2 = x0 - i2 + 2.0 * C.xxx;
        vec3 x3 = x0 - 1. + 3.0 * C.xxx;

        // Permutations
        i = mod(i, 289.0 );
        vec4 p = permute( permute( permute( i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
                                          + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
                                          + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

        // Gradients
        // ( N*N points uniformly over a square, mapped onto an octahedron.)
        float n_ = 1.0/7.0; // N=7
        vec3  ns = n_ * D.wyz - D.xzx;
        vec4 j = p - 49.0 * floor(p * ns.z *ns.z);  //  mod(p,N*N)
        vec4 x_ = floor(j * ns.z);
        vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)
        vec4 x = x_ *ns.x + ns.yyyy;
        vec4 y = y_ *ns.x + ns.yyyy;
        vec4 h = 1.0 - abs(x) - abs(y);
        vec4 b0 = vec4( x.xy, y.xy );
        vec4 b1 = vec4( x.zw, y.zw );
        //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
        //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
        vec4 s0 = floor(b0)*2.0 + 1.0;
        vec4 s1 = floor(b1)*2.0 + 1.0;
        vec4 sh = -step(h, vec4(0.0));
        vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
        vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;
        vec3 p0 = vec3(a0.xy,h.x);
        vec3 p1 = vec3(a0.zw,h.y);
        vec3 p2 = vec3(a1.xy,h.z);
        vec3 p3 = vec3(a1.zw,h.w);

        //Normalise gradients
        vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
        p0 *= norm.x;
        p1 *= norm.y;
        p2 *= norm.z;
        p3 *= norm.w;

        // Mix final noise value
        vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
        m = m * m;
        return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1),dot(p2,x2), dot(p3,x3) ) );

      }

      vec3 phongP (vec3 Kd, vec3 N, vec3 L, vec3 V) {

        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);

        float NdotL    = dot (N,L);

        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);

          diffuse  = NdotL   * (Light.Ld * Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }

        return (ambient + diffuse + specular);

      }

      vec3 phong (vec3 N, vec3 L, vec3 V) {

        vec3  ambient  = Material.Ka * Light.La;
        vec3  diffuse  = vec3(0.0);
        vec3  specular = vec3(0.0);

        float NdotL    = dot (N,L);

        if (NdotL > 0.0) {
          vec3  R       = reflect(-L, N);;
          float RdotV_n = pow(max(0.0, dot(R,V)), Material.alpha);

          diffuse  = NdotL   * (Light.Ld * Material.Kd);
          specular = RdotV_n * (Light.Ls * Material.Ks);
        }

        return (ambient + diffuse + specular);
      }

      void main() {

        vec3 n = normalize(N);
        vec3 L = normalize(Light.Position - ec);
        vec3 V = normalize(-ec);

        if(UseProcedural == 0)
        {
          //gl_FragColor = mix(texture2D(myTexture, texCoords), vec4(phong(n,L,V),1.0), 0.3);
          gl_FragColor = texture2D(myTexture, texCoords*repetition) * vec4(phong(n,L,V),1.0) * 1.6;

        }
        else if(UseProcedural == 1)
        {
          float s     = snoise(texCoords3);
          s= clamp(abs(s)*2.6,0.0,1.0);
    			vec3  newKdNoise = mix (Color1, Color2, s);
          gl_FragColor =vec4( phongP(newKdNoise, n,L,V), 1.0);
        }
      }

    </script>

  </head>

  <body>

    La: <input type="color" name="color" id="La" value="#ffffff">
    Ld: <input type="color" name="color" id="Ld" value="#ffffff">
    Ls: <input type="color" name="color" id="Ls" value="#ffffff">
    <br>
    Textura cono: <input type="file"  name="TextureFilename" accept="image/*">
    <br>
    Textura cubo: <input type="file"  name="TextureFilename" accept="image/*">
    <br>
    Repetition:   <input type="range" name="Repetition" min="1" max="10" value="1">
    <br>

    <canvas id="myCanvas" width="400" height="400">
      El Navegador no soporta HTML5
    </canvas>

    <script src="gl-matrix-min.js"></script>
    <script src="materiales.js"></script>
    <script src="primitivas.js"></script>
    <script src="Genga.js"></script>

  </body>

</html>
